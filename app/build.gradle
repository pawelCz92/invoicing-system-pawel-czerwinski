plugins {

    id 'groovy'

    id 'application'

    id 'checkstyle'

    id 'jacoco'

}

repositories {
    mavenCentral()
}

dependencies {

    testImplementation 'org.codehaus.groovy:groovy:3.0.7'

    testImplementation 'org.spockframework:spock-core:2.0-M4-groovy-3.0'

    implementation 'com.google.guava:guava:30.0-jre'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

application {
    mainClass = 'pl.futurecollars.invoicing.App'
}

checkstyle {
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled false
        csv.enabled false
        html.destination layout.buildDirectory.dir('reports/jacoco').get().asFile
    }

}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.9
            }
        }

        rule {
            limit {
                counter = 'LINE'
                minimum = 0.9
            }
        }
    }
}
tasks.register('applicationCodeCoverageReport', JacocoReport) {
    executionData run
    sourceSets sourceSets.main
}